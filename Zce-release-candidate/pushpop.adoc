<<<

[#insns-pushpop,reftext="PUSH/POP Register Instructions"]
== PUSH/POP register instructions

These instructions are collectively referred to as PUSH/POP: 

* <<#insns-c_push>> 
* <<#insns-c_pusha>> 
* <<#insns-c_pop>> 
* <<#insns-c_popret>> 
* <<#insns-c_popretz>> 

The term PUSH refers to _c.push and c.pusha_. The assembly syntax for all of these uses the mnemonic _c.push_.

The term POP refers to _c.pop_.

The term POPRET refers to _c.popret and c.popretz_.

Common details for these instructions are in this section.

The difference between _c.push_ and _c.pusha_ is whether argument registers are moved into saved registers. 
The same mnemonic is used and the difference is in the argument list.
For example:

[source,sail]
--
c.push {ra,s0-s3},          -32;# maps to c.push
c.push {ra,s0-s3}, {a0-a3}, -32;# maps to c.pusha
--

It is specified this way so the list of argument registers is explicit in the syntax, 
otherwise it would be unclear which registers were moved.

<<<
=== PUSH/POP functional overview

PUSH, POP, POPRET are used to reduce the size of function prologues and epilogues.

. The PUSH instructions
** push(store)  the registers specified in the register list to the stack frame
** _c.pusha_ also moves the registers in _areg_list_ into _s_ registers
*** In order to save encoding space _areg_list_ is determined automatically from the register list and cannot be arbitrarily specified.
*** moving argument registers into saved registers is to save them before setting up the arguments before calling the next function
** adjust the stack pointer to create the stack frame

. The POP instruction
** pops(loads) the registers in the register list from the stack frame
** adjusts the stack pointer to destroy the stack frame

. The POPRET instructions
** pop(load) the registers in the register list from the stack from
** _c.popretz_ also moves zero into into _a0_ as the return value
** adjust the stack pointer  to destroy the stack frame
** execute a _ret_ instruction to return from the function

<<<
=== Example usage

This example gives an illustration of the use of PUSH and POPRET.

The function _processMarkers_ in the EMBench benchmark picojpeg in the following file on github: https://github.com/embench/embench-iot/blob/master/src/picojpeg/libpicojpeg.c[libpicojpeg.c]

The prologue and epilogue compile with GCC10 to:

[source,SAIL]
----

   0001098a <processMarkers>:
   1098a:       711d                    addi    sp,sp,-96 ;#c.pusha(1)
   1098c:       c8ca                    sw      s2,80(sp) ;#c.pusha(2)
   1098e:       c6ce                    sw      s3,76(sp) ;#c.pusha(3)
   10990:       c4d2                    sw      s4,72(sp) ;#c.pusha(4)
   10992:       ce86                    sw      ra,92(sp) ;#c.pusha(5)
   10994:       cca2                    sw      s0,88(sp) ;#c.pusha(6)
   10996:       caa6                    sw      s1,84(sp) ;#c.pusha(7)
   10998:       c2d6                    sw      s5,68(sp) ;#c.pusha(8)
   1099a:       c0da                    sw      s6,64(sp) ;#c.pusha(9)
   1099c:       de5e                    sw      s7,60(sp) ;#c.pusha(10)
   1099e:       dc62                    sw      s8,56(sp) ;#c.pusha(11)
   109a0:       da66                    sw      s9,52(sp) ;#c.pusha(12)
   109a2:       d86a                    sw      s10,48(sp);#c.pusha(13)
   109a4:       d66e                    sw      s11,44(sp);#c.pusha(14)
   109a6:       8a2a                    mv      s4,a0     ;#c.pusha(15)
...
   109f4:       4501                    li      a0,0      ;#c.popretz(1)
   109f6:       40f6                    lw      ra,92(sp) ;#c.popretz(2)
   109f8:       4466                    lw      s0,88(sp) ;#c.popretz(3)
   109fa:       44d6                    lw      s1,84(sp) ;#c.popretz(4)
   109fc:       4946                    lw      s2,80(sp) ;#c.popretz(5)
   109fe:       49b6                    lw      s3,76(sp) ;#c.popretz(6)
   10a00:       4a26                    lw      s4,72(sp) ;#c.popretz(7)
   10a02:       4a96                    lw      s5,68(sp) ;#c.popretz(8)
   10a04:       4b06                    lw      s6,64(sp) ;#c.popretz(9)
   10a06:       5bf2                    lw      s7,60(sp) ;#c.popretz(10)
   10a08:       5c62                    lw      s8,56(sp) ;#c.popretz(11)
   10a0a:       5cd2                    lw      s9,52(sp) ;#c.popretz(12)
   10a0c:       5d42                    lw      s10,48(sp);#c.popretz(13)
   10a0e:       5db2                    lw      s11,44(sp);#c.popretz(14)
   10a10:       6125                    addi    sp,sp,96  ;#c.popretz(15)
   10a12:       8082                    ret               ;#c.popretz(16)
----

<<<

with the GCC option _-msave-restore_ the output is the following:

[source,SAIL]
----
0001080e <processMarkers>:
   1080e:       73a012ef                jal     t0,11f48 <__riscv_save_12>
   10812:       1101                    addi    sp,sp,-32
   10814:       8a2a                    mv      s4,a0

   10862:       4501                    li      a0,0
   10864:       6105                    addi    sp,sp,32
   10866:       71e0106f                j       11f84 <__riscv_restore_12>
----

with PUSH/POPRET this reduces to

[source,SAIL]
----
0001080e <processMarkers>:
   1080e:       <16-bit>                c.push    {ra,s0-s11},{a0-a3},-96

   10866:       <16-bit>                c.popretz {ra,s0-s11}, 96
----

The prologue / epilogue reduce from 62-bytes in the original code, to 16-bytes with _-msave-restore_, 
and to 4-bytes with PUSH and POPRET. 
As well as reducing the code-size PUSH and POPRET eliminate the branches from 
calling the millicode _save/restore_ routines and so also perform better. 
  
[NOTE]

  The calls to _<riscv_save_0>/<riscv_restore_0>_ become 64-bit when the target functions are out of the Â±1MB range, increasing the prologue/epilogue size to 22-bytes.

[NOTE]

  _c.pusha_ has three additional register moves included _mv s1, a1, mv s2, a2, mv s3, a3_ which weren't in the original prologue. Also, the _s4_ register needs to bhe reallocated to _s0_ to fit c.pusha.

[NOTE]

  POP is typically used in tail-calling sequences where _ret_ is not used to return to _ra_ after destroying the stack frame..

[#pushpop-areg-list]

=== Compiler implementation

The technique used in the initial implementation in LLVM is to let the compiler generate the function prologue and epilogue, 
and then replace the instruction sequences with the relevant PUSH/POP instructions.

==== _spimm_ handling

The instructions have a restricted range of _spimm_ available. If this is insufficient then a separate _c.addi16sp_ can be used to increase the range.

==== register list handling

The instructions do not directly support _{ra, s0-s10}_ to reduce the amount of encoding space required. If this register list is required then _s11_ 
should also be included. This costs a small amount of memory and performance, but saves code-size.

==== _areg_list_ handling

_c.pusha_ includes _areg_list_. This may not match what was generated by the compiler.

=== Example: _c.pusha_ fits perfectly

In this real world example generated by GCC10, _c.pusha_ fits perfectly.

[source,SAIL]
----
00e010b8 <function>:
  e010b8:       1141                    addi    sp,sp,-16 ; #c.pusha
  e010ba:       c422                    sw      s0,8(sp)  ; #c.pusha
  e010bc:       c226                    sw      s1,4(sp)  ; #c.pusha
  e010be:       c04a                    sw      s2,0(sp)  ; #c.pusha
  e010c0:       c606                    sw      ra,12(sp) ; #c.pusha
  e010c2:       842a                    mv      s0,a0     ; #c.pusha
  e010c4:       84ae                    mv      s1,a1     ; #c.pusha
  e010c6:       4908                    lw      a0,16(a0)
  e010c8:       4d8c                    lw      a1,24(a1)
  e010ca:       8932                    mv      s2,a2     ; #c.pusha
  e010cc:       726040ef                jal     ra,e057f2 <function2>
----

this is replaced by

[source,SAIL]
----
00e010b8 <function1>:
  e010b8:       xxxx                    c.push {ra,s0-s2}, {a0-a2}, -16
  e010c6:       4908                    lw      a0,16(a0)
  e010c8:       4d8c                    lw      a1,24(a1)
  e010cc:       726040ef                jal     ra,e057f2 <function2>
----

=== Example: _areg_list_ doesn't fit

In this other real world example _areg_list_ doesn't fit:

[source,SAIL]
----
00e01126 <function3>:
  e01126:       1101                    addi    sp,sp,-32 ; #c.push
  e01128:       ce06                    sw      ra,28(sp) ; #c.push
  e0112a:       cc22                    sw      s0,24(sp) ; #c.push
  e0112c:       ca26                    sw      s1,20(sp) ; #c.push
  e0112e:       c84a                    sw      s2,16(sp) ; #c.push
  e01130:       c64e                    sw      s3,12(sp) ; #c.push
  e01132:       c452                    sw      s4,8(sp)  ; #c.push
  e01134:       c256                    sw      s5,4(sp)  ; #c.push
  e01136:       c05a                    sw      s6,0(sp)  ; #c.push
  e01138:       0e050363                beqz    a0,e0121e <function3+0xf8>
  e0113c:       8a2a                    mv      s4,a0
  e0113e:       852e                    mv      a0,a1
  e01140:       89ae                    mv      s3,a1
----

In this case, the move instructions are not part of the same basic block so _c.push_ is selected:

[source,SAIL]
----
00e01126 <function4>:
  e01126:       xxxx                    c.push {ra,s0-s6}, -32 
  e01138:       0e050363                beqz    a0,e0121e <function4+0xf8>
  e0113c:       8a2a                    mv      s4,a0
  e0113e:       852e                    mv      a0,a1
  e01140:       89ae                    mv      s3,a1
----

=== Example: _areg_list_ needs register allocation changes

The next case is where none of the register moves match the _areg_list_ moves because the register allocator in the compiler did not allocate suitable registers:

[source,SAIL]
----
00e01842 <function5>:

  e01e7e:       1101                    addi    sp,sp,-32
  e01e80:       cc22                    sw      s0,24(sp)
  e01e82:       c84a                    sw      s2,16(sp)
  e01e84:       c64e                    sw      s3,12(sp)
  e01e86:       c452                    sw      s4,8(sp)
  e01e88:       c256                    sw      s5,4(sp)
  e01e8a:       ce06                    sw      ra,28(sp)
  e01e8c:       ca26                    sw      s1,20(sp)
  e01e8e:       892a                    mv      s2,a0
  e01e90:       89ae                    mv      s3,a1
  e01e92:       8a32                    mv      s4,a2
  e01e94:       8ab6                    mv      s5,a3
  e01e96:       3f41                    jal     e01e26 <function6>
----

With _c.pusha_ this becomes:

[source,SAIL]
----
e01e7e <function5>:
  # c.push includes moving {a0-a3} into {s0-s3}
  e01e7e:       1101                    c.push {ra,s0-s5}, {a0-a3}, -32
  e01e8e:       892a                    mv      s2,a0;# <-- switch dest to s0
  e01e90:       89ae                    mv      s3,a1;# <-- switch dest to s1
  e01e92:       8a32                    mv      s4,a2;# <-- switch dest to s2
  e01e94:       8ab6                    mv      s5,a3;# <-- switch dest to s3
  e01e96:       3f41                    jal     e01e26 <function6>
----

In this case all four moves can be deleted if the register allocation can be altered.
if the register allocation _cannot_ be altered, then _c.push_ should be used instead.

=== Example: _areg_list_ partially fits

In this final case, one register move can be deleted and one must be retained unless the register allocation can be changed.

[source,SAIL]
----
00e02368 <function7>:
  e02368:       1141                    addi    sp,sp,-16
  e0236a:       c226                    sw      s1,4(sp)
  e0236c:       03450493                addi    s1,a0,52
  e02370:       c422                    sw      s0,8(sp)
  e02372:       842a                    mv      s0,a0;# <-- delete this one
  e02374:       8526                    mv      a0,s1;# <-- doesn't fit areg_list
  e02376:       c04a                    sw      s2,0(sp)
  e02378:       c606                    sw      ra,12(sp)
  e0237a:       892e                    mv      s2,a1;# <-- switch dest to s1
  e0237c:       df3fd0ef                jal     ra,e0016e <function8>
----

[source,SAIL]
----
00e02368 <function7>:
  e02368:       xxxx                    c.push {ra,s0-s2}, {a0-a2}, -16
  e0236c:       03450493                addi    s1,a0,52
  e02374:       8526                    mv      a0,s1;# <-- doesn't fit areg_list
  e0237a:       892e                    mv      s2,a1;# <-- switch dest to s1
  e0237c:       df3fd0ef                jal     ra,e0016e <function8>
----

In this case one move is deleted, but one remains because unless the target register can be reallocated.

For the smallest code-size the compiler should reallocate the target registers so that the moves in _areg_list_ are not wasted.

[#pushpop-fault-handling]
=== PUSH/POP Fault handling

The sequence required to execute the PUSH/POP instruction may be interrupted, or may not be able to start execution for several reasons.

* virtual memory page fault or PMP fault
** these can be detected before execution, or during execution if the memory addresses cross a page/PMP boundary
** xTVAL is set to any address which causes the fault
* watchpoint trigger
** these can be detected before execution, or during execution depending on the trigger type (load data triggers require the sequence to have started executing, for example)
** xTVAL is set to any address which causes the fault
* external debug halt
** the halt can treat the whole sequence atomically, or interrupt mid sequence (implementation defined)
* debug halt caused by a trigger
** same comment as watchpoint trigger above
* load access fault
** these are detected while the sequence is executing
** xTVAL is set to the fault address.
* store access fault (precise or imprecise)
** these may be detected while the sequence is executing, or afterwards if imprecise
** xTVAL is set to the fault address.
* interrupts
** these may arrive at any time. An implementation can choose whether to interrupt the sequence or not.

[NOTE]

  xTVAL may be hardwired to zero in an implementation. Recovering from faults such as page faults requires that it is implemented.

In all cases MEPC contains the PC of the PUSH/POP instruction, and MCAUSE is set as expected for the type of fault.

For debug halts DPC is set to the PC of the PUSH/POP instruction.

Because some faults can only be detected during the sequence the core implementation must be able to recover from the fault and re-execute the sequence. 
This may involve executing some or all of the loads and stores from the sequence multiple times before the sequence completes (as multiple faults or multiple interrupts are possible).

Therefore correct execution requires that _sp_ refers to idempotent memory (also see <<pushpop_non-idem-mem>>).

[#pushpop-software-view]
=== Software view of execution

==== Software view of the PUSH sequence

From a software perspective the PUSH sequence appears as:

* A sequence of stores writing a contiguous block of memory. Any of the bytes may be written multiple times.
* An optional series of register moves
* A stack pointer adjustment

Because the memory is idempotent and the stores are non-overlapping, they may be reordered, grouped into larger accesses, split into smaller access or any combination of these.

If an implementation allows interrupts during the sequence, and the interrupt handler uses _sp_ to allocate stack memory, then any stores which were executed before the interrupt may be overwritten by the handler. 
This is safe because the memory is idempotent and the stores will be re-executed when execution resumes.

The stack pointer adjustment must only be committed once it is certain that all of the stores will complete without triggerring any precise faults (for example, page faults). 
Stores may also return imprecise faults from the bus. It is platform defined whether the core implementation waits for the bus responses before continuing to the final stage of the sequence, 
or handles errors responses after completing the PUSH instruction.

<<<

For example:

[source,sail]
--
c.push  {ra, s0-s5}, {a0-a3}, -64
--

Appears to software as:

[source,sail]
--
# any bytes from sp-1 to sp-28 may be written multiple times before the instruction completes
# therefore these updates may be visible in the interrupt/exception handler below the stack pointer
sw  s5, -4(sp);   
sw  s4, -8(sp);   
sw  s3,-12(sp);   
sw  s2,-16(sp);  
sw  s1,-20(sp);   
sw  s0,-24(sp);   
sw  ra,-28(sp);   

# these must only execute once, and will only execute after all stores completed without any precise faults
# all instructions must execute atomically
# therefore these updates are not visible in the interrupt/exception handler
mv   s0, a0
mv   s1, a1
mv   s2, a2
mv   s3, a3
addi sp, sp, -64; 
--

==== Software view of the POP/POPRET sequence

From a software perspective the POP/POPRET sequence appears as:

* A sequence of loads, any of which may be executed multiple times
* A stack pointer adjustment
* An optional LI zero into a0
* An optional RET

If an implementation allows interrupts during the sequence, then any loads which were executed before the interrupt may update architectural state. 
The loads will be re-executed once the handler completes, so the values will be overwritten. 
Therefore it is permitted for an implementation to update some of the destination registers before taking the interrupt or other fault.

The optional load immediate and stack pointer adjustment must only be committed once it is certain that all of the loads will complete successfully.

For POPRET once the stack pointer adjustment has been committed the RET must execute.

<<<
For example:

[source,sail]
--
c.popretz {ra, s0-s3}, 32 ; 
--

Appears to software as:

[source,sail]
--
# any or all of these load instructions may execute multiple times
# therefore these updates may be visible in the interrupt/exception handler
lw   s3, 28(sp);
lw   s2, 24(sp);
lw   s1, 20(sp);
lw   s0, 16(sp);
lw   ra, 12(sp);

# must only execute once, will only execute after all loads complete successfully
# all instructions must execute atomically
# therefore these updates are not visible in the interrupt/exception handler
li a0, 0
addi sp, sp, 32;
ret;
--

=== Forward progress guarantee

The PUSH/POP sequence has the same forward progress guarantee as executing the instructions from the equivalent assembly sequences.

[[pushpop_non-idem-mem]]
=== Non-idempotent memory handling

An implementation may have a requirement to issue a PUSH/POP instruction to non-idempotent memory. 

==== Error detection

If the core implementation does not support PUSH/POP to non-idempotent memories, the core may use an idempotency PMA to detect it and take a 
load(POP/POPRET) or store (PUSH) access fault exception in order to avoid unpredictable results.

==== Non-idempotent support

It is possible to support non-idempotent memory. One reason is to re-use PUSH/POP as a restricted form of a load/store multiple instruction to a peripheral,
as there is no generic load/store multiple instruction in the RISC-V ISA. 

If accessing non-idempotent memory then it is _recommended_ to:

. Not allow interrupts during execution
. Not allow external debug halt during execution
. Detect any virtual memory page faults or PMP faults for the whole instruction before starting execution (instead of during the sequence)
. Not split / merge / reorder the generated memory accesses

It is possible that one of the following will still occur during execution:

. Watchpoint trigger
. Load/store access fault

In these cases the core will jump to the debug or exception handler. If execution is required to continue afterwards (so the event is not fatal to the code execution), then the handler is required to do so in software. 

By following these rules memory accesses will only ever be issued once, and decreasing address order.

It is possible for implementations to follow these restricted rules and to safely access both types of memory. 
It is also possible for an implementation to use PMAs to detect the memory type and apply different rules, such as only allowing interrupts if accessing cacheable memory, for example.

include::Zces_footer.adoc[]
