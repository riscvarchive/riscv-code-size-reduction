= RISC-V Push/Pop Extension (UABI only version)
Version 0.1
:doctype: book
:encoding: utf-8
:lang: en
:toc: left
:toclevels: 4
:numbered:
:xrefstyle: short
:le: &#8804;
:rarr: &#8658;

This document describesï¼š

* 32-bit encodings for `PUSH/PUSH.[FDQ]`, `POP/POP.[FDQ]` and `POPRET/POPRET.[FDQ]`
* 16-bit encodings for `C.PUSH, C.POP` and `C.POPRET` instructions 

The instructions are defined for RV32, RV64 and RV128 architectures I variants only, using the UABI. 

Future revisions may include:

* The E variant
* EABI support

These instructions are to reduce the code-size of function prologues and epilogues by using a single encoding to replace a sequence of instructions. 
They are designed to replace the most common function prologue/epilogue sequences, but will not replace all possible cases. 

== A brief example from Zephyr

This example gives a nice illustration of what `PUSH/POP` is trying to achieve.

[source,sourceCode,text]
----
from subsys/bluetooth/controller/crypto/crypto.c
int bt_rand(void *buf, size_t len)
{
        return lll_csrand_get(buf, len);
}
----

compiles with GCC10 to:

[source,sourceCode,text]
----
20405458 <bt_rand>:
20405458:	1141                	addi	sp,sp,-16	;#PUSH(1)
2040545a:	c04a                	sw	s2,0(sp)	;#PUSH(2)
2040545c:	70000937          	lui	s2,0x70000
20405460:	62090613          	addi	a2,s2,1568 # 70000620 <prng>
20405464:	c422                	sw	s0,8(sp)	;#PUSH(3)
20405466:	c226                	sw	s1,4(sp)	;#PUSH(4)
20405468:	c606                	sw	ra,12(sp)	;#PUSH(5)
2040546a:	842a                	mv	s0,a0		;#PUSH(6)
2040546c:	84ae                	mv	s1,a1		;#PUSH(7)
<function body>
20405494:	4501                	li	a0,0		;#POPRET(1)
20405496:	40b2                	lw	ra,12(sp)	;#POPRET(2)
20405498:	4422                	lw	s0,8(sp)	;#POPRET(3)
2040549a:	4492                	lw	s1,4(sp)	;#POPRET(4)
2040549c:	4902                	lw	s2,0(sp)	;#POPRET(5)
2040549e:	0141                	addi	sp,sp,16	;#POPRET(6)
204054a0:	8082                	ret			;#POPRET(7)
----

with the GCC option `msave-restore` the output is the following:

[source,sourceCode,text]
----
204089ac <bt_rand>:
204089ac:       f97f72ef                jal     t0,20400942 <__riscv_save_0>	;#PUSH(1)
204089b0:       70001937                lui     s2,0x70001
204089b4:       ac090613                addi    a2,s2,-1344 # 70000ac0 <prng>
204089b8:       842a                    mv      s0,a0	;#PUSH(2)
204089ba:       84ae                    mv      s1,a1	;#PUSH(3)
<function_body>
204089e2:       4501                    li      a0,0	;#POPRET(1)
204089e4:       f83f706f                j       20400966 <__riscv_restore_0>	;#POPRET(2)
----

with `PUSH/POP` this reduces to

[source,sourceCode,text]
----
20405458 <bt_rand>:
20405458:	<16-bit>                push	 {ra,s0-s2},{a0-a1},-16
2040545c:	70000937          	lui	s2,0x70000
20405460:	62090613          	addi	a2,s2,1568 # 70000620 <prng>
<function body>
20405496:	<16-bit>                popret	 {ra,s0-s2},{0} 16

----

The prologue / epilogue reduce from 28-bytes in the original code, to 14-bytes with `-msave-restore`, and to to 8-bytes with `PUSH/POP`, which will also improve the performance.

[NOTE]

  We should also develop a better optimised version of `-msave-restore`
  
[NOTE]

  The calls to `<riscv_save_0>/<riscv_restore_0>` become 64-bit when the target functions are out of the +/-1MB range, increasing the prologue/epilogue size to 22-bytes.


== Open issues

it's better to list these first, as they are resolved I'll update the proposal

* allocate encodings for the 32-bit versions
* whether the fault handling should be profile defined

== Assembler Syntax

[source,sourceCode,text]
----
//these encodings may be 16-bit or 32-bit depending upon the parameters
push           {<rsreg_list>},[{<areg_list>},] -<stack_adjustment>
pop            {<rsreg_list>},[{ret_val},]      <stack_adjustment> 
popret         {<rsreg_list>},[{ret_val},]      <stack_adjustment> 
----

. The `PUSH` instruction 
** pushes(stores)  the registers specified in `rsreg_list` to the stack
** if `areg_list` is included, moves the registers in the `areg_list` into `s` registers
** adjusts the stack pointer by the `stack_adjustment` 

. The `POP` instruction 
** pops(loads) the registers in `rsreg_list to the stack` from the stack
** if `ret_val` is included, moves the specified value into `a0` as the return value
** adjusts the stack pointer by the `stack_adjustment`.

. `POPRET` has the same behaviour as `POP`, followed by `RET`.

[source,sourceCode,text]
----
//these encodings are 32-bit as F registers are included in the list
//the length of the F registers is specified in the opcode
push.[fdq]     {<fsreg_list>}, -<stack_adjustment> 
pop.[fdq]      {<fsreg_list>},  <stack_adjustment> 
popret.[fdq]   {<fsreg_list>},  <stack_adjustment> 
----

These instructions allow `fs` registers to be pushed/popped and do not allow `areg_list` or `ret_val` to be specified (to save encoding space).

32-bit and 16-bit forms are available, the assembler should choose the 16-bit form if the parameters permit and the C extension is implemented.

The registers in `reg_list` and `areg_list` are comma separated lists and must not be empty.

[source,sourceCode,text]
----
<sreg_list>  ::= <ra> "," <sreg_range> | <ra> | <sreg_range>
<rsreg_list> ::= <ra> "," <sreg_range> | <ra>
<fsreg_list> ::= <ra> "," <sreg_range> ", " <freg_range> | <ra> "," <freg_range> | <sreg_range> "," <freg_range> | <freg_range>
<rfsreg_list> ::= <ra> "," <sreg_range> ", " <freg_range> | <ra> "," <freg_range>
<areg_list>  ::= <areg_range>
<ret_val>    ::= s0 | 0 | 1
<sreg_range> ::= <s0>  | <s0-sN>   (where N is in the range [1, 11])
<freg_range> ::= <fs0> | <fs0-fsM> (valid values of M are 1,2,3,5,8,11)
<areg_range> ::= <a0>  | <a0-aP>   (where P is in the range [1, 2])
<reg_list>   ::= <sreg_list>  | <rsreg_list> | <fsreg_list> | <rfsreg_list>

//legal syntax

push         {<sreg_list>},  [{<areg_list>}],-stack_adjustment
push.[fdq]   {<fsreg_list>},                 -stack_adjustment

pop          {<sreg_list>},  [{ret_val},]    stack_adjustment
pop.[fdq]    {<fsreg_list>},                 stack_adjustment

popret       {<rsreg_list>}, [{ret_val},]    stack_adjustment //popret must include ra
popret.[fdq] {<rfsreg_list>},                stack_adjustment //popret must include ra

----

For the 16-bit encodings

. In `reg_list`
.. `ra` must be included
.. `s0-sN` may be included, valid values of `N` are 0,1,2,3,5,8,11
.. `fs0-fsM` must not be included
. for `pop, ret_val` must not be included 
. for `popret, ret_val` may be included - and can only be set to `0`
.. about 27% of functions which infer `popret` issue `li a0, 0` first
. for `push` only specific combinations of `areg_list` and `sreg_list` are permitted
.. `if sreg_list={ra}:       areg_list={}`
.. `if sreg_list={ra,s0}:    areg_list={a0}`
.. `if sreg_list={ra,s0-sN}: areg_list={a0-a1} where N=1,2,3`
.. `if sreg_list={ra,s0-sN}: areg_list={a0-a2} where N=5,8,11`
. The `stack_adjustment` must be no more than the total memory required for `reg_list`, rounded up to a multiple of 16-bytes, plus an additional (0..7)*16-bytes.

Examples of valid 16-bit encodings for RV32 or RV64:

[source,sourceCode,text]
----

push   {ra},                 -16; //store ra;                                      decrement sp by 16
push   {ra,s0},    {a0},     -32; //store ra,s0;     mv s0,a0;                     decrement sp by 32
push   {ra,s0-s3}, {a0-a1},  -96; //store ra,s0-s3;  mv s0,a0; mv s1,a1;           decrement sp by 96
push   {ra,s0-s11},{a0-a2}, -128; //store ra,s0-s11; mv s0,a0; mv s1,a1; mv s2,a2; decrement sp by 128

pop    {ra},        16;  //load  ra;        increment sp by 16
popret {ra},        16;  //load  ra;        increment sp by 16; jump to ra

pop    {ra,s0},     32;  //load  ra,s0;     increment sp by 32
popret {ra,s0},{s0},32;  //load  ra,s0;     increment sp by 32; mv a0, s0; jump to ra

pop    {ra,s0-s3},  96;  //load  ra,s0-s3;  increment sp by 96
popret {ra,s0-s3},  96;  //load  ra,s0-s3;  increment sp by 96; jump to ra

pop    {ra,s0-s11}, 128; //load  ra,s0-s11; increment sp by 128
popret {ra,s0-s11}, 128; //load  ra,s0-s11; increment sp by 128; jump to ra

----
[NOTE]
  There is no encoding space in the 16-bit `c.push` encoding to explicitly specify `areg_list` so it is automatically determined from the `reg_list` value based on analysing usage in the benchmark suite


[NOTE]
  To save encoding space some opcodes do not allow the full range of `spimm` or `reg_list` which reduces the possible `stack adjustment` range further, see <<#16bitencodings>>

For the 32-bit encodings

. The `stack_adjustment` must be no more than the total memory required for `reg_list`, rounded up to a multiple of 16-bytes, plus an additional (0..15)*16-bytes.

For example

. `push   {ra,s0-s2}, {a0-a1}, -64` can use a 16-bit encoding
. `push   {s0-s2},    {a0-a1}, -64` must use a 32-bit encoding as `ra` is not in the register list
. `push   {ra,s0-s2}, {a0-a1}, -256` must use a 32-bit encoding as the `stack_adjustment` is out of range
. `push   {ra,s0-s2},          -32` must use a 32-bit encoding as `sreg_list` requires `areg_list={a0-a1}` for the 16-bit encoding
. `push.f {ra,s0-s2, fs0},     -64` must use a 32-bit encoding as `fs0` is in the register list
. `pop    {ra,s0-s2}, {s0},     32` must use a 32-bit encoding as `ret_val` is specified
. `popret {ra,s0-s2}, {s0},     32` must use a 32-bit encoding as `c.popret` only supports `0` as the `ret_val` value

To be a legal encoding:

1.  The stack adjustment must be negative for all `PUSH` variants and positive for all `POP[RET]` variants
2.  The stack adjustment must be in range (see <<spimm>>) and must be a multiple of 16
3.  The register lists must be valid (see above)
4.  If any `f` registers are included in the list there must be an `.[fdq]` suffix
5.  It there is an `.[fdq]` suffix, there must be `f` registers in the list
6.  It there is an `.[fdq]` suffix, `areg_list` must not be included in the synax
7.  For `POPRET` variants, `rsreg_list` must include `ra`
8.  `areg_list` must not be included for all `POP` and `POPRET` variants and for `PUSH.[FDQ]`

Additionally:

. If `sp` is not 16-byte aligned then take a load (`push`) or store (`pop[ret]`) address misaligned exception, and do not issue any loads or stores.

[NOTE]
  When moving from loading or storing `x` registers to `f` registers there may be a gap as the registers may be different widths, and all registers must be aligned in memory

== Determining XLEN and FLEN to decode and execute the instructions

The width of `x` registers in the register list depends upon XLEN and so `misa.MXL`. From the ELF file header, XLEN is determined by the following flags:

* ELFCLASS64 for RV64
* ELFCLASS32 for RV32 

[NOTE]

  I don't think there's an ELFCLASS128 for RV128

If using a debugger then `misa.MXL` can be read, if XLEN is not known for the core.

FLEN is explicitly specified in the opcode as the presence of any `f` registers requires a `.[fdq]` suffix on the opcode.

Follow this link for details of the https://github.com/riscv/riscv-elf-psabi-doc/blob/master/riscv-elf.md#file-header[ELF file header]

=== Rationale: why include the .[fdq] suffix

It is required to hard-code the length of the `f` registers in the opcode, and not just save/restore the whole register. We must allow code compiled for the F-extension
to run on a  core which supports D and possibly Q. We must also allow code compiled for the D-extension to run on a core which supports Q.

If we don't explicitly state the `f` register width and always store the whole register, then the registers will be stored at different addresses to the ones expected 
by the compiler. 

An alternative would be to use `misa.[FDQ]`
to directly control the behaviour and so set `misa.[DQ]=0` when running F code. Nothing else in the architecture requires this, so it seems unreasonable to 
add this requirement here.

== Pseudo-code

The pseudo-code below show the required architectural state updates. 

Note that every architectural state update in the pseudo-code can be executed as a standard RISC-V 32-bit encoding, so that it is possible execute the instructions as sequences of standard instructions. 

Because the pseudo-code includes load or store operations, they may take any fault caused by executing loads or stores. See <<fault-handling>> for more details.

=== PUSH Pseudo-code

All variants of the `PUSH` instruction store the set of registers from `reg_list` to consecutive memory locations, and decrement the stack pointer.
The pseudo-code uses assembly inserts so that it can use `fsw/fsd` etc.

The pseudo-code shows the memory and architectural state updates of the whole instruction which has completed without faults, debug halts or interrupts. See <<fault-handling>> and <<software-view>> for more information.

[NOTE]
  `stack_adjustment` is negative.

[source,sourceCode,text]
----
//sp must be correctly aligned
if (sp[3:0])     {take_illegal_instruction_exception();}
if (misa.MXL==1) {bytes=4;}
if (misa.MXL==2) {bytes=8;}
else             {bytes=16;}
addr=sp;
if (ra) {
  addr-=bytes;
  switch(bytes) {
    4:  asm("sw ra, 0(addr)");
    8:  asm("sd ra, 0(addr)");
    16: asm("sq ra, 0(addr)");
  }
}
for(i in sreg_list)  {
  addr-=bytes;
  switch(bytes) {
    4:  asm("sw s[i], 0(addr)");
    8:  asm("sd s[i], 0(addr)");
    16: asm("sq s[i], 0(addr)");
  }
}
if (freq_list) {
  switch (opcode) {
    //if FLEN > XLEN, then get the address back into alignment before saving the F registers
    "PUSH.D": while(addr% 8) {addr-=bytes;}
    "PUSH.Q": while(addr%16) {addr-=bytes;}
  }
  for(i in freg_list)  {
    switch (opcode) {
      "PUSH.F": {addr-= 4;asm("fsw fs[i], 0(addr)");}
      "PUSH.D": {addr-= 8;asm("fsd fs[i], 0(addr)");}
      "PUSH.Q": {addr-=16;asm("fsq fs[i], 0(addr)");}
    }
  }
}
if (areg_list) {
  for (i in areg_list) {
    asm("mv s[i], a[i]");}
  }
}
sp+=stack_adjustment; //decrement
----

=== POP[RET] Pseudo-code

A `POP[RET]` instruction loads the set of registers from `reg_list` from consecutive memory locations, and then increments the stack pointer. 
The pseudo-code uses assembly inserts so that it can use `flw/fld/ret`.

The pseudo-code shows the architectural state updates of the whole instruction which has completed without faults, debug halts or interrupts. See <<fault-handling>> and <<software-view>> for more information.

[NOTE]
  `stack_adjustment` is positive.

[source,sourceCode,text]
----
//sp must be correctly aligned
if (sp[3:0])     {take_illegal_instruction_exception();}
if (misa.MXL==1) {bytes=4;}
if (misa.MXL==2) {bytes=8;}
else             {bytes=16;}
addr=sp+stack_adjustment;
if (ra) {
  addr-=bytes;
  switch(bytes) {
    4:  asm("lw ra, 0(addr)");
    8:  asm("ld ra, 0(addr)");
    16: asm("lq ra, 0(addr)");

  }
}
for(i in sreg_list)  {
  addr-=bytes;
  switch(bytes) {
    4:  asm("lw s[i], 0(addr)");
    8:  asm("ld s[i], 0(addr)");
    16: asm("lq s[i], 0(addr)");
  }
}
if (freq_list) {
  switch (opcode) {
    //if FLEN > XLEN, then get the address back into alignment before saving the F registers
    "POP.D", "POPRET.D": while(addr% 8) {addr-=bytes;}
    "POP.Q", "POPRET.Q": while(addr%16) {addr-=bytes;}
  }
  for(i in freg_list)  {
    switch (opcode) {
      "POP.F", "POPRET.F": {addr-= 4;asm("flw fs[i], 0(addr)");}
      "POP.D", "POPRET.D": {addr-= 8;asm("fld fs[i], 0(addr)");}
      "POP.Q", "POPRET.Q": {addr-=16;asm("flq fs[i], 0(addr)");}
    }
  }
}
if (ret_val) {
   switch(ret_val) {
      "s0": asm("mv a0, s0");
      "0":  asm("li a0, 0");
      "1":  asm("li a0, 1");
   }
}
sp+=stack_adjustment; //increment
if (opcode == "POPRET") { 
   asm("ret");
}
----

== 32-bit Encodings

.proposed 32-bit encodings
[options="header",width="100%"]
|============================================================================
|31:29  |28:27|26|25:24   |23:20  |19:15 |14:12 |11:7  |6:0     |name
|xxx    |xx   |ra|mv      |rcount |spimm |000   |xxxxx |xxxxxxx |PUSH
|xxx    |xx   |ra|ret_val |rcount |spimm |001   |xxxxx |xxxxxxx |POP
|xxx    |xx   |x |ret_val |rcount |spimm |010   |xxxxx |xxxxxxx |POPRET

|xxx    |00   |ra|frcount |rcount |spimm |000   |xxxxx |xxxxxxx |PUSH.F
|xxx    |00   |ra|frcount |rcount |spimm |001   |xxxxx |xxxxxxx |POP.F
|xxx    |00   |x |frcount |rcount |spimm |010   |xxxxx |xxxxxxx |POPRET.F

|xxx    |01   |ra|frcount |rcount |spimm |000   |xxxxx |xxxxxxx |PUSH.D
|xxx    |01   |ra|frcount |rcount |spimm |001   |xxxxx |xxxxxxx |POP.D
|xxx    |01   |x |frcount |rcount |spimm |010   |xxxxx |xxxxxxx |POPRET.D

|xxx    |10   |ra|frcount |rcount |spimm |000   |xxxxx |xxxxxxx |PUSH.Q
|xxx    |10   |ra|frcount |rcount |spimm |001   |xxxxx |xxxxxxx |POP.Q
|xxx    |10   |x |frcount |rcount |spimm |010   |xxxxx |xxxxxxx |POPRET.Q

|============================================================================

[NOTE]
  bits [28:27] are the `flen` field
  `ra=1` for all `popret` encodings, it is not specified in the encoding

=== Illegal instructions for the 32-bit encodings

The encoding takes the same behaviour as any floating point instruction if executed when disabled:

* if `frcount>0` and `misa.F==0`
* if `frcount>0` and `flen==1` and `misa.D==0`
* if `frcount>0` and `flen==2` and `misa.Q==0`

The following cases do not decode as `PUSH*/POP*/POPRET*`

* No registers are specified (`rcount, frcount` are both zero)
* `rcount>14`
* `flen=3`
* `rcount==13` and `frcount>0` and the opcode doesn't have an `[fdq]` suffix

==== Rational: `rcount=13, frcount=0` saving all saved registers

The debian distro does save all `s` and `fs` registers on a few occasions, so I used a special encoding for this case. I saw no justification for saving all `fs` registers *without* saving all `s` registers. It seems useful to have a form which saves all saved registers.

The programmable range for `frcount` doesn't save many `fs` registers as `FPMark` only saves one or two and Debian rarely saves more than that.


[#16bitencodings]
== 16-bit Encodings

.proposed 16-bit encodings
[options="header",width="100%"]
|=============================================================================================
|15 |14 |13 |12 |11 |10  |9 |8|7    |6   |5   |4|3|2        |1 |0 |instruction
3+|100      |1  |0  |0   |0 2+|rcount|0   |0 2+|00 |spimm 2+|00   |C.POP
3+|100      |1  |0  |0 3+|rcount    |ret0|1 3+|spimm      2+|00   |C.POPRET
3+|110      |1  |0  |0 3+|rcount    |1   |0 3+|spimm      2+|00   |C.PUSH
|=============================================================================================

[NOTE]
  * For `C.POPRET`, `ret_val[0]=ret0` as specified in the encoding, `ret_val[1]=0`. For `C.POP` `ret_val[1:0]=0`. If `ret0==1` then return zero and return.
  * Some encodings remain *reserved* for the time being so we can add them later if they can be justified
  ** For `C.POP`, `spimm[2:1]=0`, and `rcount[2]=0` are reserved, as these encodings give minimal benefit
  ** For `C.PUSH/C.POPRET`, if `rcount[2]=1` then `spimm[2]=0` as these encodings give minimal benefit

== Encoding Fields

[#spimm]
=== `spimm` encoding field

The `stack_adjustment` field in the assembly syntax comprises of two components:

. the memory required for the registers in the list, rounded up to 16-bytes (using the `Align16` function below)
. additional stack space allocated for local variables, encoded in the `spimm` field

The 16-bit encoding allows up to 7 additional 16-byte blocks (as `spimm` has up to 3-bits), and the 32-bit encoding allows up to 31.

[NOTE]
  we may change the size of `spimm` in the 32-bit encoding once benchmarking work has completed

[source,sourceCode,text]
----
//the padding calculation requires SP to be 16-byte aligned before executing the instruction
if (FLEN>XLEN && 
    number_of_registers_in_freg_list>0 && 
    number_of_registers_in_reg_list % (FLEN/XLEN)>0) {
  //need padding to ensure FLEN registers are aligned
  padding = (FLEN/XLEN - number_of_registers_in_reg_list % (FLEN/XLEN)) * XLEN/8;
} else {
  padding = 0
}
total_register_bytes = number_of_registers_in_reg_list * XLEN/8 + padding + number_of_registers_in_freg_list * FLEN/8;
stack_adjustment = Align16(total_register_bytes) + 16*spimm;
----

The bytes required for padding is simple - if FLEN==XLEN it's always zero. If FLEN=2*XLEN it is 0 or 4. RV32Q is more complex (see below) but this is an unlikely configuration.
`n` is an integer in the tables so `2n` is always even, `2n+1` is always odd.

[#padding-examples-RV32D]
.RV32D padding examples, XLEN=32, FLEN=64
[options="header"]
|====================================
| #Xreg  |Padding required if accessing Fregs
| 2n     |none                     
| 2n+1   |4-bytes
|====================================

[#padding-examples-RV64Q]
.RV64Q padding examples, XLEN=64, FLEN=128
[options="header"]
|====================================
| #Xreg  |Padding required if accessing Fregs
| 2n     |none                     
| 2n+1  |4-bytes
|====================================

[#padding-examples-RV32Q]
.RV64Q padding examples, XLEN=32, FLEN=128
[options="header"]
|====================================
| #Xreg  |Padding required if accessing Fregs
| 4n     |none                     
| 4n+1   |4-bytes
| 4n+2   |8-bytes                     
| 4n+3   |12-bytes
|====================================

=== `mv, ret_val, ra, frcount, rcount` for the 32-bit encodings

The registers in the `reg_list` are controlled by these fields

Note that `frcount`, `mv` and `ret_val` are in the same location in the encoding.

[#32bit-frcount]
.`frcount` values for the 32-bit encodings for `PUSH.[FDQ], POP.[FDQ], POPRET.[FDQ]`
[options="header"]
|=====================
|frcount | ABI names  
| 0      |none or `fs0-fs11` (see `rcount=13`)
| 1      |fs0         
| 2      |fs0-fs1
| 3      |fs0-fs5
|=====================

[#32bit-mv]
.`mv` values for the 32-bit encodings for `PUSH`
[options="header"]
|=========================================
|mv      |ABI names  
| 0      |none        
| 1      |a0
| 2      |a0-a1
| 3      |a0-a2
|=========================================

[#32bit-ret_val]
.`ret_val` values for the 32-bit encodings for `POP, POPRET`
[options="header"]
|=========================================
|return value      |ABI names  
| 0      |none (don't set a0)        
| 1      |a0=0
| 2      |a0=1
| 3      |a0=s0
|=========================================

[#32bit-ra]
.`ra` values for the 32-bit encodings 
[options="header"]
|=========================================
|ra      |ABI names  
| 0      |none        
| 1      |ra
|=========================================

[#32bit-rcount]
.`rcount` field values for the 32-bit encodings
[options="header"]
|==========================
|rcount  | ABI names      
| 0      |none       
| 1      |s0
| 2      |s0-s1
| 3      |s0-s2
| 4      |s0-s3
| 5      |s0-s4
| 6      |s0-s5
| 7      |s0-s6
| 8      |s0-s7
| 9      |s0-s8
| 10     |s0-s9
| 11     |s0-s10
| 12     |s0-s11
2+|  `rcount=13` is valid if `frcount=0` for `[.fdq]` opcodes
| 13     |s0-s11,fs0-fs11
2+|these values could be used for interrupt save/restore
| 14-15  | *reserved*
|==========================

=== `rcount`, `ret_val` for the 16-bit encodings

[#rcount-table]
.`rcount` values for the 16-bit encodings
[options="header",width=100%]
|============================
|rcount| ABI names
|0     |ra
|1     |ra, s0
|2     |ra, s0-s1
|3     |ra, s0-s2
2+|Following options for `C.PUSH/C.POPRET` only
|4     |ra, s0-s3
|5     |ra, s0-s5
|6     |ra, s0-s8
|7     |ra, s0-s11
|============================

`ret_val` is as specified in <<32bit-ret_val>>. Note that:

* `C.POPRET` allows bit 0 to vary, bit 1 is always 0
* `C.POP` always has `ret_val=0` to save encoding space

[#fault-handling]
== Fault handling

The sequence required to execute the instruction may be interrupted, or may not be able to start execution for several reasons.

* virtual memory page fault or PMP fault
** these can be detected before execution, or during execution if the memory addresses cross a page/PMP boundary
* watchpoint trigger
** these can be detected before execution, or during execution depending on the trigger type (load data triggers require the sequence to have started executing, for example)
* external debug halt
** the halt can treat the whole sequence atomically, or interrupt mid sequence (implementation defined)
* debug halt caused by a trigger
** same comment as watchpoint trigger above
* load access fault
** these are detected while the sequence is executing
* store access fault (precise or imprecise)
** these may be detected while the sequence is executing, or afterwards if imprecise
* interrupts
** these may arrive at any time. An implementation can choose whether to interrupt the sequence or not.

Because some faults can only be detected during the sequence the core implementation is able to recover from the fault and re-execute the sequence. This may involve executing some or all of the loads and stores from the sequence multiple times before the sequence completes (as multiple faults or multiple interrupts are possible).

Therefore correct execution requires that `sp` refers to idempotent memory (see <<non-idem-mem>> for non-idempotent handling)

[#software-view]
== Software view of execution

=== Software view of `PUSH` sequence

From a software perspective the `PUSH` sequence appears as:

* A sequence of stores writing a contiguous block of memory. Any of the bytes may be written multiple times.
* A stack pointer adjustment

Because the memory is idempotent and the stores are non-overlapping, they may be reordered, grouped into larger accesses, split into smaller access or any combination of these.

If an implementation allows interrupts during the sequence, and the interrupt handler uses `sp` to allocate stack memory, then any stores which were executed before the interrupt maybe be overwritten by the handler. This is safe because the memory is idempotent and the stores will be re-executed once the handler completes.

The stack pointer adjustment must only be committed once it is certain that all of the stores will complete within triggerring any precise faults (stores may return imprecise bus errors which are received after the instruction has completed execution).

Referring to the example in <<push-example-1>> 

[source,sourceCode,text]
----
c.push  {ra, s0-s5},{a0-a2}, -64
----

Appears to software as:

[source,sourceCode,text]
----
# any bytes from SP-1 to SP-28 may be written multiple times before the instruction completes
sw  ra, -4(sp);   
sw  s0, -8(sp);   
sw  s1,-12(sp);   
sw  s2,-16(sp);  
sw  s3,-20(sp);   
sw  s4,-24(sp);   
sw  s5,-28(sp);   

# these must only execute once, and will only execute after all stores complete sucessfully
mv s0, a0;
mv s1, a1;
mv s2, a2;
addi sp, sp, -64; 
----

=== Software view of `POP[RET]` sequence

From a software perspective the `POP[RET]` sequence appears as:

* A sequence of loads, any of which may be executed multiple times
* A stack pointer adjustment
* An optional `RET`

If an implementation allows interrupts during the sequence, then any loads which were executed before the interrupt may update architectural state. The loads will be re-executed once the handler completes, so the values will be overwritten. Therefore it is permitted for an implementation to update some of the destination registers before taking the interrupt or other fault.

The register moves and stack pointer adjustment must only be committed once it is certain that all of the loads will complete successfully.

For `POPRET` once the stack pointer adjustment has been committed the `RET` must execute.

Referring to the example in <<popret-example-1>> 

[source,sourceCode,text]
----
popret   {ra, s0-s3}, {s1}, 32 ; 
----

Appears to software as:

[source,sourceCode,text]
----
# any or all of these load instructions may execute multiple times
lw   ra, 28(sp);
lw   s0, 24(sp);
lw   s1, 20(sp);
lw   s2, 16(sp);
lw   s3, 12(sp);
flw  fs0, 8(sp);
flw  fs1, 4(s0);

# must only execute once, will only execute after all loads complete successfully
# all instructions must execute atomically
mv a0, s1
addi sp, sp, 32;
ret;
----
[[non-idem-mem]]
=== Non-idempotent memory

An implementation may have a requirement to issue `PUSH/POP` to non-idempotent memory. 

==== Error detection

If the core implemetnation does not have a requirement to support `PUSH/POP` to non-idempotent memories then take a load(`POP[RET]`) or store (`PUSH`) access fault exception.

==== Non-idempotent support

It is possible to support non-idempotent memory. One reason is to re-use `PUSH/POP` as a restricted form of a load/store multiple instruction to a peripheral (see <<ldstm>>), as there is no generic load/store multiple instruction currently in the RISC-V ISA. If load/store multiple is suported in the future then it should support non-idempotent memory.

Non-idempotent memory support requires additional restrictions:

. Do not allow interrupts during execution
. Do not allow external debug halt during execution
. Detect any virtual memory page faults or PMP faults for the whole instruction before starting execution (instead of during the sequence)
. Do not split / merge / reorder the generated memory accesses

It is possible that one of the following will still occur during execution:

. Watchpoint trigger
. Load/store access fault

In these cases the core will jump to the debug or exception handler. If execution is required to continue afterwards (so the event is not fatal to the code execution), then the handler software is required to do so in software. 

By following these rules memory accesses will only ever be issued once, and in the order listed in the pseudo-code.

It is possible for implementations to follow these restricted rules and to safely access both types of memory. It is also possible for an implementation to use PMAs to detect the memory type and apply different rules, such as only allowing interrupts if accessing cacheable memory for example.

== Toolchain implementation

`PUSH/POP` can be generated by the compiler, but can also be added as an optimisation in the linker. If they are inferred at link time then they can also be used to optimise precompiled library code. This means that the libraries can be shipped without these instructions, but platforms which implement them can still benefit from the code-size saving.

== Initial benchmarking results

See https://github.com/riscv/riscv-code-size-reduction/blob/master/ISA%20proposals/Huawei/README.adoc[PUSH+MV/POP] in on this page.
Benchmarking not yet updated for `mv a0, s0` included in `c.popret`

== Assembly examples

[#push-example-1]
=== C.PUSH RV32 UABI (16-byte aligned stack)

[source,sourceCode,text]
----
c.push  {ra, s0-s5}, {a0-a2}, -64
----

Encoding: rcount=5, spimm=2

Equivalent sequence:

[source,sourceCode,text]
----
sw  ra, -4(sp);
sw  s0, -8(sp); 
sw  s1, -12(sp);
sw  s2, -16(sp); 
sw  s3, -20(sp);
sw  s4, -24(sp); 
sw  s5, -28(sp);
mv  s0, a0;
mv  s1, a1;
mv  s2, a2;
addi sp, sp, -64;
----

=== C.PUSH RV32I UABI (16-byte aligned stack)

[source,sourceCode,text]
----
c.push {ra, s0-s1}, {a0-a1} -32
----

Encoding: rcount=2, spimm=1

Equivalent sequence:

[source,sourceCode,text]
----
sw  ra, -4(sp);
sw  s0, -8(sp); 
sw  s1, -12(sp);
mv s0, a0;
mv s1, a1;
addi sp, sp, -32;
----

=== C.POP RV32I UABI (16-byte aligned stack)

[source,sourceCode,text]
----
c.pop   {ra, s0-s7}, 160
----

Encoding: rcount=6, spimm=7 

Equivalent sequence:

[source,sourceCode,text]
----
lw   ra, 156(sp);
lw   s0, 152(sp);  
lw   s1, 148(sp);  
lw   s2, 144(sp);  
lw   s3, 140(sp);  
lw   s4, 136(sp);  
lw   s5, 132(sp);  
lw   s6, 128(sp);  
lw   s7, 124(sp);  
lw   s8, 120(sp);  
addi sp, sp, 160
----

=== C.POPRET RV32I UABI (16-byte aligned stack)

[source,sourceCode,text]
----
c.pop   {ra, s0-s7},{s0}, 160
----

Encoding: rcount=6, spimm=7 

Equivalent sequence:

[source,sourceCode,text]
----
lw   ra, 156(sp);
lw   s0, 152(sp);  
lw   s1, 148(sp);  
lw   s2, 144(sp);  
lw   s3, 140(sp);  
lw   s4, 136(sp);  
lw   s5, 132(sp);  
lw   s6, 128(sp);  
lw   s7, 124(sp);  
lw   s8, 120(sp);  
addi sp, sp, 160;
mv a0, s0;
ret
----

=== PUSH.F RV32I[FDQ] UABI (16-byte aligned stack)

[source,sourceCode,text]
----
push.f  {ra, s0-s4, fs0}, -64
----

Encoding: ra=1, rcount=5, frcount=1, flen=0, spimm=2 (16-byte aligned)

Micro operation sequence:

[source,sourceCode,text]
----
sw  ra,   -4(sp);
sw  s0,   -8(sp); 
sw  s1,  -12(sp); 
sw  s2,  -16(sp); 
sw  s3,  -20(sp); 
sw  s4,  -24(sp); 
fsw fs0, -28(sp)
addi sp, sp, -64;
----

=== PUSH.D RV64I[DQ] UABI (16-byte aligned stack)

[source,sourceCode,text]
----
push.d  {ra, s0-s4, fs0}, -64
----

Encoding: ra=1, rcount=5, frcount=1, flen=1, spimm=0 (16-byte aligned)

Micro operation sequence:

[source,sourceCode,text]
----
sd  ra,  -8(sp);
sd  s0,  -16(sp); 
sd  s1,  -24(sp);
sd  s2,  -32(sp); 
sd  s3,  -40(sp);
sd  s4,  -48(sp); 
fsd fs0, -56(sp)
addi sp, sp, -64;
----

=== PUSH.D RV64I[DQ] UABI (16-byte aligned stack)

[source,sourceCode,text]
----
push.d  {fs0-fs7}, -128
----

Encoding: rcount=0, frcount=12, flen=2, spimm=2 (16-byte aligned)

Micro operation sequence:

[source,sourceCode,text]
----
fsd  fs0,  -8(sp);
fsd  fs1, -16(sp); 
fsd  fs2, -24(sp);
fsd  fs3, -32(sp); 
fsd  fs5, -40(sp); 
fsd  fs4, -48(sp);
fsd  fs6, -56(sp);
fsd  fs7, -64(sp); 
fsd  fs8, -72(sp); 
fsd  fs9, -80(sp); 
fsd  fs10,-88(sp); 
fsd  fs11,-96(sp); 
addi sp, sp, -128; 
----

=== POP RV32I UABI (16-byte aligned stack)

[source,sourceCode,text]
----
pop   {ra, s0-s9}, {s0}, 256
----

Encoding: ra=1, rcount=10, frcount=0, spimm=13 (16-byte aligned)

Micro operation sequence:

[source,sourceCode,text]
----
lw   ra, 252(sp);
lw   s0, 248(sp);
lw   s1, 244(sp);  
lw   s2, 240(sp)
lw   s3, 236(sp);  
lw   s4, 232(sp);
lw   s5, 228(sp);  
lw   s6, 224(sp)
lw   s7, 220(sp);  
lw   s8, 216(sp);
lw   s9, 212(sp); 
mv   a0, s0
addi sp, sp, 256
----

[#popret-example-1]
=== POPRET.F RV32I[FDQ] UABI (16-byte aligned stack)

[source,sourceCode,text]
----
popret.f   {ra, s0-s3, fs0-fs1}, 32
----

Encoding: ra=1, rcount=4, frcount=2, flen=1, spimm=0 (16-byte aligned)

Micro operation sequence:

[source,sourceCode,text]
----
lw   ra, 28(sp);
lw   s0, 24(sp);
lw   s1, 20(sp);  
lw   s2, 16(sp);
lw   s3, 12(sp);  
flw  fs0, 8(sp);
flw  fs1, 4(s0);  
addi sp, sp, 32; 
ret
----

[[ldstm]]
== Appendix: Usage as load/store-multiple

`PUSH/POP` encodings can be reused as generic load/store multiple instructions. For example:

[source,sourceCode,text]
----

lui t0, 0xffff
sw  s5, 4c(t0)
sw  s4, 50(t0)
sw  s3, 54(t0)
sw  s2, 58(t0)
sw  s1, 5c(t0)
sw  s0, 60(t0)

----

Can be replaced by

[source,sourceCode,text]
----

lui t0, 0xffff
addi sp, t0, 64
push {s0-s5}, -64

----

This requires that  `sp` is saved before and restored after the sequence. Also `POP` can be used to replace the equivalent series of loads. 

Even though there is not much code-size saving once the `sp` save/restore code has been included, and the restricted memory layout targetted by the instructions, there may be a performance benefit to doing this for small embedded cores. A load/store-multiple sequence may be issued to the peripheral bus as a single burst instead of multiple single accesses which individually require arbitration. In some cases this can save one or more cycles per access as the arbiter remains locked to the core until the sequence has completed.

If the target memory is non-idempotent then see <<non-idem-mem>> for additional constraints.


